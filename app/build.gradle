apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

Properties localProps = new Properties()
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(localPropsFile.newDataInputStream())
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    signingConfigs {
        travis {
            storeFile file("debug.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "Lawnchair"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        applicationId "com.zhaisoft.mylauncher"

        buildConfigField "boolean", "TRAVIS", System.getenv("TRAVIS") ?: "false"
        buildConfigField "boolean", "TAGGED_BUILD", System.getenv("TRAVIS_TAG") ? "true" : "false"
        buildConfigField "int", "TRAVIS_BUILD_NUMBER", System.getenv("TRAVIS_BUILD_NUMBER") ?: "0"
        buildConfigField "boolean", "ENABLE_LAWNFEED", "false"

        ext.awarenessApiKey = localProps.getProperty('awarenessApiKey') ?: System.getenv("AWARENESS_API_KEY")
        buildConfigField "boolean", "AWARENESS_API_ENABLED", ext.awarenessApiKey ? "true" : "false"
        manifestPlaceholders = [awarenessApiKey: ext.awarenessApiKey ?: "null"]

        if (System.getenv("TRAVIS") == "true") {
            versionCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
            versionName = System.getenv("MAJOR_MINOR") + (System.getenv("DRONE") ? "-" : ".") + System.getenv("TRAVIS_BUILD_NUMBER")
        } else {
            versionCode 2
            versionName "1.0.2"
        }
    }

    buildTypes {
        debug {
            if (System.getenv("TRAVIS") == "true") {
                minifyEnabled true
                shrinkResources true
                if (System.getenv("DRONE") == "true") {
                    applicationIdSuffix ".ci"
                }

                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                if (System.getenv("TRAVIS_EVENT_TYPE") != "pull_request") {
                    signingConfig signingConfigs.travis
                }
            } else {
                minifyEnabled false
            }
        }

        release {
            //applicationIdSuffix ".plah"
            buildConfigField "boolean", "ENABLE_LAWNFEED", "true"

            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (System.getenv("TRAVIS_EVENT_TYPE") != "pull_request") {
                signingConfig signingConfigs.travis
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.resValue "string", "application_id", variant.applicationId
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    def supportLibVersion = '27.1.1'
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:palette-v7:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:preference-v14:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:support-dynamic-animation:${supportLibVersion}"
    implementation 'com.github.KwabenBerko:OpenWeatherMap-Android-Library:v1.1.1'
    implementation 'com.google.android.gms:play-services-awareness:15.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.pavelsikun:vintage-chroma:1.5'
    implementation project(':launcherclient')
    implementation project(':hotspot')

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.github.florent37:fiftyshadesof:1.0.0'
    implementation 'me.jfenn:attribouter:0.0.5'


    implementation 'com.zhaisoft.lib.updater:android-version-updater:0.1.14'

}

repositories {
    mavenCentral()
}
